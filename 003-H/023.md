Bent Menthol Turtle

high

# Invariants will fail when funding fee sets `stableCollateralTotal` to zero

## Summary

Since the new changes to the code, now the PnL of the leveraged positions is not settled until the individual positions are closed. This will cause the variable `stableCollateralTotal` to be set to zero by the funding fees (even though the collateral owned by LPs is not zero), causing a discrepancy between the tracked balances and the new balances. This discrepancy will cause the invariant checks to revert, causing a DoS on all orders and liquidations. 

## Vulnerability Detail

Imagine the following scenario, the price of ETH goes down significantly, causing an increase in the collateral owned by the LPs. If traders don't close their positions, the losses from the positions won't be realized and the variable `stableCollateralTotal` won't represent the current collateral owned by the LPs. Then, is possible that some LPs withdraw liquidity, driving `stableCollateralTotal` to almost zero (even though the actual collateral owned by the LPs is way bigger because of the unrealized loss of the traders).

In the scenario given above, if we add that the funding fee is paying traders when the function `settleFundingFees` is called (always before any operation), the value of `stableCollateralTotal` will be negative, but is capped at zero so it will be set to zero:

https://github.com/sherlock-audit/2024-03-flat-money-fix-review-contest/blob/main/flatcoin-v1/src/FlatcoinVault.sol#L442-L448
```solidity
    function _updateStableCollateralTotal(int256 _stableCollateralAdjustment) private {
        int256 newStableCollateralTotal = int256(stableCollateralTotal) + _stableCollateralAdjustment;

        // The stable collateral shouldn't be negative as the other calculations which depend on this
        // will behave in unexpected manners.
        stableCollateralTotal = (newStableCollateralTotal > 0) ? uint256(newStableCollateralTotal) : 0;
    }
```

Capping `stableCollateralTotal` at zero will cause a desync between `stableCollateralTotal` and `marginDepositedTotal`, causing a discrepancy between the tracked funds (`stableCollateralTotal` + `marginDepositedTotal`) and the actual funds (`collateral.balanceOf(vault)`).

When this discrepancy is given, the invariant checks that are executed before and after any operation will fail, reverting the whole transaction. Theses checks are the following:

First, we get the collateral net before and after the operation:
https://github.com/sherlock-audit/2024-03-flat-money-fix-review-contest/blob/main/flatcoin-v1/src/misc/InvariantChecks.sol#L94-L102
```solidity
    function _getCollateralNet(IFlatcoinVault vault) private view returns (int256 netCollateral) {
        int256 collateralBalance = int256(vault.collateral().balanceOf(address(vault)));
        int256 trackedCollateral = int256(vault.stableCollateralTotal()) +
            vault.getGlobalPositions().marginDepositedTotal;

        if (collateralBalance < trackedCollateral) revert FlatcoinErrors.InvariantViolation("collateralNet1");

        return collateralBalance - trackedCollateral;
    }
```

Then, we compare that the collateral net before and after the operation doesn't differ too much, only allowing a difference of `1e6` for rounding:
https://github.com/sherlock-audit/2024-03-flat-money-fix-review-contest/blob/main/flatcoin-v1/src/misc/InvariantChecks.sol#L109-L115
```solidity
    /// @dev Collateral balance changes should match tracked collateral changes
    function _collateralNetBalanceRemainsUnchanged(int256 netBefore, int256 netAfter) private pure {
        // Note: +1e6 to account for rounding errors.
        // This means we are ok with a small margin of error such that netAfter - 1e6 <= netBefore <= netAfter.
        if (netBefore > netAfter || netAfter > netBefore + 1e6)
            revert FlatcoinErrors.InvariantViolation("collateralNet2");
    }
```

The key is that the first collateral net is got before the function `settleFundingFees` is executed. When `stableCollateralTotal` is near zero and the funding fee is paying traders with a value bigger than `1e6`, the invariant will revert because of the discrepancy on the collateral net. 

**Sidenote:**
After some thinking, I've realized that when `stableCollateralTotal` is near zero, there are more causes apart from the funding fee that can bring `stableCollateralTotal` to a negative value, capping it at zero and causing the same DoS. The funding fee is only ONE scenario where `stableCollateralTotal` can be capped at zero, other scenarios could be adjusting or closing a position, realizing some profits.

## Impact

When the funding fee (or other cause) sets the variable `stableCollateralTotal` to a negative value, capping it at zero, the invariant checks will fail, causing a DoS on the `executeOrder` and `liquidate` functions. The highest impact will be the DoS on the `liquidate` function, because it will prevent unhealthy positions from being liquidated, causing bad debt on the market. 

## Code Snippet

https://github.com/sherlock-audit/2024-03-flat-money-fix-review-contest/blob/main/flatcoin-v1/src/FlatcoinVault.sol#L447

## Tool used

Manual Review

## Recommendation

It's recommended to always settle the funding fees before the execution of the invariant checks. 

```diff
    modifier orderInvariantChecks(IFlatcoinVault vault) {
        IStableModule stableModule = IStableModule(vault.moduleAddress(FlatcoinModuleKeys._STABLE_MODULE_KEY));
+       vault.settleFundingFees();

        // ...
    }

    modifier liquidationInvariantChecks(IFlatcoinVault vault, uint256 tokenId) {
        IStableModule stableModule = IStableModule(vault.moduleAddress(FlatcoinModuleKeys._STABLE_MODULE_KEY));
+       vault.settleFundingFees();

        // ...
    }

```

Also, I'd recommend allowing the variable `stableCollateralTotal` to be set to a negative value to deal with these edge cases. That is because there may be more actions during an operation that may bring `stableCollateralTotal` to zero, causing the invariant checks to revert. 
